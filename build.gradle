plugins {
    id 'com.github.hierynomus.license' version '0.11.0'
}

// Default tasks
defaultTasks 'licenseFormat', 'build'

allprojects {
    apply plugin: 'java'

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    apply plugin: 'com.github.hierynomus.license'

    // Basic project information
    group = 'org.spongepowered'
    version = '1.0.0'

    // Minimum version of Java required
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    // Project repositories and dependencies
    repositories {
        mavenCentral()
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
        maven {
            name = 'Maven Repository for JDT Compiler release'
            url = 'https://repo.eclipse.org/content/repositories/eclipse-staging/'
        }
    }

    // License header formatting
    license {
        ext {
            name = rootProject.projectName
            organization = rootProject.organization
            url = rootProject.url
        }

        header rootProject.file('HEADER.txt')
        include '**/*.java'
        exclude '**/spoon/fixed/**'
        ignoreFailures false
        strictCheck true

        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    // Source compiler configuration
    configure([compileJava, compileTestJava]) {
        options.compilerArgs += ['-Xlint:all', '-Xlint:-path']
        options.deprecation = true
        options.encoding = 'UTF-8'
    }

    // JAR manifest configuration
    jar {
        manifest {
            attributes(
                    'Built-By': System.properties['user.name'],
                    'Created-By': "${System.properties['java.vm.version']} (${System.properties['java.vm.vendor']})",
                    'Implementation-Name': project.name,
                    'Implementation-Version': project.version,
                    'Implementation-Vendor': project.url,
            )
        }
    }

    // Deployment configuration
    apply from: rootProject.file('gradle/deploy.gradle')
}

apply plugin: 'java-gradle-plugin'

dependencies {
    compile project(':event-gen-core')
    compile 'fr.inria.gforge.spoon:spoon-core:4.4.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
